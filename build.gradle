buildscript {
    ext {
        springBootVersion = '1.5.1.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/snapshot' }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'http://repository.pentaho.org/artifactory/repo' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'com.moowork.node' version '1.1.1'
    id 'com.moowork.gulp' version '1.1.1'
    id 'org.flywaydb.flyway' version '4.1.2'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'http://repository.pentaho.org/artifactory/repo' }
}

final frontendDir = "${project.projectDir}/frontend"
final staticDir = "${project.projectDir}/static"
final schemaDir = "${project.projectDir}/xsd"
final staticSchemaDir = staticDir + '/schema'

def updateSchemaLocation(final def filePath) {
    final proj = file(filePath)
    final text = proj.getText("UTF-8")
            .replaceAll('"common.xsd"', '"https://internal.emc.com/reserv-io/schema/common.xsd"')
            .replaceAll('"register.xsd"', '"https://internal.emc.com/reserv-io/schema/register.xsd"')
            .replaceAll('"report.xsd"', '"https://internal.emc.com/reserv-io/schema/report.xsd"')
    proj.write(text, "UTF-8")
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: 'xjc', classname: 'org.jvnet.jaxb2_commons.xjc.XJC2Task',
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir) {
                schema(dir: schemaDir, includes: "api.xsd")
                arg(value: '-wsdl')
                arg(value: '-XtoString')
                arg(value: '-Xequals')
                arg(value: '-XhashCode')
                arg(value: '-p')
                arg(value: 'com.emc.internal.reserv.dto')
                produces(dir: sourcesDir, includes: '**/*.java')
            }

            javac(destdir: classesDir,
                    source: sourceCompatibility,
                    target: sourceCompatibility,
                    debug: true,
                    includeantruntime: false,
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: '**/*.java')
                include(name: '*.java')
            }

            //noinspection GroovyAssignabilityCheck
            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: '**/*.java')
                }
            }

            //noinspection GroovyAssignabilityCheck
            copy(todir: staticSchemaDir) {
                fileset(dir: schemaDir, erroronmissingdir: false)
            }
        }

        new FileNameFinder().getFileNames(staticSchemaDir, '*.xsd').each {
            updateSchemaLocation(it)
        }
    }
}

jar {
    baseName = 'reserv-io'
    version = '0.0.1-SNAPSHOT'
    //noinspection GroovyAssignabilityCheck
    from genJaxb.classesDir
}

configurations {
    all*.exclude module: 'spring-boot-starter-logging'
    all*.exclude group: 'javax.servlet', module: 'servlet-api'
    providedRuntime
    jaxb
}

bootRun {
    //noinspection GroovyAssignabilityCheck
    systemProperties = System.properties
}

test {
    //noinspection GroovyAssignabilityCheck
    systemProperties = System.properties
}

final springSessionVersion = '1.3.0.RELEASE'
final springBootVersion = '1.5.1.RELEASE'
final springSecurityOAuth2Version = '2.1.0.RELEASE'
final mysqlVersion = '6.0.5'
final lombokVersion = '1.16.14'
final hibernateVersion = '5.2.8.Final'
final hibernateValidatorVersion = '5.4.0.Final'
final jacksonVersion = '2.8.6'
final guavaVersion = '21.0'
final wsdl4jVersion = '1.6.3'
final activityVersion = '5.22.0'
final jaxbVersion = '2.2.11'
final jaxb2CommonsLangVersion = '2.3'
final jaxb2BasicsVersion = '0.11.1'
final apacheCommonsLangVersion = '3.5'
final jade4jVersion = '1.1.4'
final pentahoVersion = '4.3.1.8-209'
final olap4jVersion = '1.2.0'

node {
    version = '7.7.2'
    npmVersion = '4.4.1'
    distBaseUrl = 'https://nodejs.org/dist'
    download = true
    workDir = file(frontendDir + '/nodejs')
    nodeModulesDir = file(frontendDir)
}

gulp {
    workDir = file(frontendDir)
    colors = true
    bufferOutput = true
}

dependencies {
    //noinspection GroovyAssignabilityCheck
    compile(
            [group: 'org.springframework.boot', name: 'spring-boot-starter', version: springBootVersion],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc', version: springBootVersion],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2', version: springBootVersion],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: springBootVersion],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: springBootVersion],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: springBootVersion],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-web-services', version: springBootVersion],

            [group: 'org.hibernate', name: 'hibernate-core', version: hibernateVersion],
            [group: 'org.hibernate', name: 'hibernate-entitymanager', version: hibernateVersion],
            [group: 'org.hibernate', name: 'hibernate-validator', version: hibernateValidatorVersion],

            [group: 'org.springframework.session', name: 'spring-session', version: springSessionVersion],
            [group: 'org.springframework.session', name: 'spring-session-jdbc', version: springSessionVersion],
            [group: 'org.springframework.security.oauth', name: 'spring-security-oauth2', version: springSecurityOAuth2Version],

            [group: 'org.activiti', name: 'activiti-spring-boot-starter-jpa', version: activityVersion],
            [group: 'org.activiti', name: 'activiti-spring-boot-starter-basic', version: activityVersion],

            [group: 'pentaho', name: 'mondrian', version: pentahoVersion],
            [group: 'org.olap4j', name: 'olap4j', version: olap4jVersion],

            [group: 'wsdl4j', name: 'wsdl4j', version: wsdl4jVersion],
            [group: 'com.google.guava', name: 'guava', version: guavaVersion],
            [group: 'de.neuland-bfi', name: 'spring-jade4j', version: jade4jVersion],
            [group: 'org.jvnet.jaxb2_commons', name: 'jaxb2-basics', version: jaxb2BasicsVersion],
            [group: 'org.apache.commons', name: 'commons-lang3', version: apacheCommonsLangVersion],
            [group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: jacksonVersion],
            files(genJaxb.classesDir).builtBy(genJaxb)
    )
    runtime(
            [group: 'mysql', name: 'mysql-connector-java', version: mysqlVersion],
    )
    compileOnly(
            [group: 'org.projectlombok', name: 'lombok', version: lombokVersion],
    )
    providedRuntime(
            [group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version: springBootVersion],
    )
    testCompile(
            [group: 'junit', name: 'junit', version: '4.12'],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion],
    )
    jaxb(
            [group: 'org.glassfish.jaxb', name: 'jaxb-xjc', version: jaxbVersion],
            [group: 'org.jvnet.jaxb2_commons', name: 'jaxb2-basics', version: jaxb2BasicsVersion],
            [group: 'org.jvnet.jaxb2_commons', name: 'jaxb2-basics-ant', version: jaxb2BasicsVersion],
            [group: 'org.jvnet.jaxb2_commons', name: 'jaxb2-commons-lang', version: jaxb2CommonsLangVersion],
    )
}

task prepareEnvironment {
    doLast {
        nodeSetup.execute()
        npmSetup.execute()
        npmInstall.execute()
        installGulp.execute()
    }
}

task cleanStatic {
    doLast {
        delete frontendDir + '/build'
        delete "${project.projectDir}/static"
    }
}

clean.dependsOn(cleanStatic)

task gulpBuild(type: GulpTask) {
    args = ['build']
}

task copyBundles(type: Copy) {
    from frontendDir + '/build'
    into "${project.projectDir}/static"
}

task rebuildFrontend {
    doLast {
        cleanStatic.execute()
        gulpBuild.execute()
        copyBundles.execute()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4'
}

flyway {
    driver = 'com.mysql.cj.jdbc.Driver'
    url = 'jdbc:mysql://localhost:3306/reserv-io?useSSL=true&useJDBCCompliantTimezoneShift=true&serverTimezone=UTC'
    user = 'root'
    password = 'YEPYEPYEP'
    baselineVersion = '0'
    baselineDescription = 'Flyway init'
    schemas = ['reserv-io']
    baselineOnMigrate = true
    locations = ["filesystem:${project.projectDir}/schema_versions/mysql"]
}